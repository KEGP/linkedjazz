PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>
PREFIX fx: <http://sparql.xyz/facade-x/ns/>

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX : <http://localhost:8080/ontology/kejazz/>
PREFIX res: <http://localhost:8080/resource/kejazz/>
PREFIX dul: <http://www.ontologydesignpatterns.org/ont/dul/DUL.owl#>

CONSTRUCT {
  [ a :RelationBetweenArtists;
    :includesSourceArtist ?sourceArtistMyNS;
    :includesTargetArtist ?targetArtistMyNS;
    :atTime [
      a dul:TimeInterval;
      :hasIntervalStartDate ?startDate;
      :hasIntervalEndDate ?endDate.
    ];
    dul:satisfies ?aaRelationMyNS.
  ].

  # RELATED ENTITIES
  ?targetArtistMyNS a :Artist; # The target artist may not be generated by the artist.sparql query!
    :hasMusicBrainzID ?targetID.

  ?aaRelationMyNS a :RelationBetweenArtistsType;
    owl:sameAs ?musicbrainzRelationURL;
    rdfs:label ?relationType;
    :hasMusicBrainzID ?relationTypeID.

} WHERE {
  SERVICE<x-sparql-anything:>{
    # launch on musicbrainz/artist/
    fx:properties fx:location ?_name ;
                  fx:trim-strings "true"^^xsd:boolean .

    ?root a fx:root;
          xyz:artist ?artist_BN.

    {
      ?artist_BN xyz:id ?sourceID;
                 xyz:artist-relation-list/fx:anySlot ?target_BN.
      ?target_BN xyz:target ?targetID.
      ?target_BN xyz:direction "forward".
    }
    UNION
    {
      ?artist_BN xyz:id ?targetID;
                 xyz:artist-relation-list/fx:anySlot ?target_BN.
      ?target_BN xyz:direction "backward".
    }

    OPTIONAL{?target_BN xyz:type ?relationType.}
    OPTIONAL{?target_BN xyz:type-id ?relationTypeID.}
    OPTIONAL{?target_BN xyz:begin ?startDate.}
    OPTIONAL{?target_BN xyz:end ?endDate.}

    BIND(fx:entity(res:Artist, "/", ?sourceID) as ?sourceArtistMyNS).
    BIND(fx:entity(res:Artist, "/", ?targetID) as ?targetArtistMyNS).
    #BIND(fx:entity(res:RelationBetweenArtistsType, "/", REPLACE(?relationType, " ","_")) as ?aaRelationMyNS).
    BIND(fx:entity(res:RelationBetweenArtistsType, "/", ?relationTypeID) as ?aaRelationMyNS).

    BIND(fx:entity("https://musicbrainz.org/relationship/", ?relationTypeID) as ?musicbrainzRelationURL).
  }
}
